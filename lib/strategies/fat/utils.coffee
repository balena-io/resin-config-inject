_ = require('lodash')
fs = require('fs')
errors = require('resin-errors')

###*
# @summary Check if a number is divisible by another number
# @protected
# @function
#
# @param {Number} x - x
# @param {Number} y - y
#
# @throws If either x or y are zero.
#
# @example
# utils.isDivisibleBy(4, 2)
###
exports.isDivisibleBy = (x, y) ->
	if x is 0 or y is 0
		throw new Error('Numbers can\'t be zero')

	return not (x % y)

###*
# @summary Copy a file to specific start point of another file
# @protected
# @function
#
# @description It uses streams.
#
# @param {String} file - input file path
# @param {String} output - output file path
# @param {Number} start - byte start
# @param {Function} callback - callback (error, output)
#
# @example
# utils.streamFileToPosition 'input/file', 'output/file', 1024, (error) ->
#		throw error if error?
###
exports.streamFileToPosition = (file, output, start, callback) ->

	if not file?
		throw new errors.ResinMissingParameter('file')

	if not _.isString(file)
		throw new errors.ResinInvalidParameter('file', file, 'not a string')

	if not output?
		throw new errors.ResinMissingParameter('output')

	if not _.isString(output)
		throw new errors.ResinInvalidParameter('output', output, 'not a string')

	if not start?
		throw new errors.ResinMissingParameter('start')

	if not _.isNumber(start)
		throw new errors.ResinInvalidParameter('start', start, 'not a number')

	if not callback?
		throw new errors.ResinMissingParameter('callback')

	if not _.isFunction(callback)
		throw new errors.ResinInvalidParameter('callback', callback, 'not a function')

	fs.exists file, (exists) ->

		if not exists
			return callback(new Error("File does not exist: #{file}"))

		inputStream = fs.createReadStream(file)
		inputStream.on('error', callback)

		outputStream = fs.createWriteStream output,
			start: start

			# The default flag is 'w', which replaces the whole file
			flags: 'r+'

		outputStream.on('error', callback)

		outputStream.on 'close', ->
			return callback(null, output)

		inputStream.pipe(outputStream)
